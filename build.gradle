
apply plugin: 'application'
apply plugin: 'java'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

mainClassName = 'org.kontalk.Kontalk'

buildDir = 'build_gradle'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

evaluationDependsOn ":client-common-java"

ext.bcVersion = '1.52'
ext.smackVersion = project(':client-common-java').smackVersion

dependencies {
    compile project(':client-common-java')

    compile fileTree(dir: 'lib', include: ['*.jar'])

    compile group: 'org.bouncycastle', name: 'bcpg-jdk15on', version: "$bcVersion"
    compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: "$bcVersion"
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: "1.1.1"
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: "4.3.5"
    compile group: 'org.apache.httpcomponents', name: 'httpcore', version: "4.3.2"
    compile group: 'commons-configuration', name: 'commons-configuration', version: "1.10"
    //compile group: 'commons-lang', name: 'commons-lang', version: "2.6"
    compile group: 'commons-io', name: 'commons-io', version: "2.4"
    compile group: 'org.jcraft', name: 'jorbis', version: "0.0.17"
    compile group: 'org.ocpsoft.prettytime', name: 'prettytime', version: "3.2.7.Final"
    compile group: 'org.igniterealtime.smack', name: 'smack-java7', version: "$smackVersion"
    compile group: 'org.xerial', name: 'sqlite-jdbc', version: "3.8.6"
    compile group: 'de.sciss', name: 'weblaf', version: "1.27"
}

repositories {
    mavenCentral()
}

task copyDeps(type: Copy) {
    from(configurations.runtime)
    into project.file('dist/lib')
}

jar {
    baseName = 'KontalkDesktopApp'
    destinationDir project.file('dist')
    
    manifest {
        attributes(
            "Main-Class": mainClassName,
            "Class-Path": configurations.compile.collect { 'lib/'+it.getName() }.join(' ')
        )
    }
    
    dependsOn ':copyDeps'
}
