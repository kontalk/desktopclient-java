
apply plugin: 'application'
apply plugin: 'java'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

mainClassName = 'org.kontalk.Kontalk'
ext.clientCommonDir = 'client-common-java'

applicationDefaultJvmArgs = ['''-Djava.util.logging.SimpleFormatter.format=%1$tH:%1$tM:%1$tS|%4$-6s|%2$s-%5$s%6$s%n''']

if (!file('./'+clientCommonDir+'/build.gradle').exists()) {
    throw new GradleException('Submodule not found. Run "git submodule update --init".')
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

evaluationDependsOn ':'+clientCommonDir

ext.bcVersion = '1.60'
ext.smackVersion = project(':'+clientCommonDir).smackVersion

//configurations.all { transitive = false }

dependencies {
    compile project(':'+clientCommonDir)

    compile group: 'org.bouncycastle', name: 'bcpg-jdk15on', version: "$bcVersion"
    compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: "$bcVersion"
    compile group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: "$bcVersion"
    compile group: 'commons-cli', name: 'commons-cli', version: "1.3.1"
    compile group: 'commons-codec', name: 'commons-codec', version: "1.10"
    compile group: 'commons-configuration', name: 'commons-configuration', version: "1.10"
    compile group: 'commons-io', name: 'commons-io', version: "2.4"
    compile group: 'commons-lang', name: 'commons-lang', version: "2.6"
    compile group: 'commons-logging', name: 'commons-logging', version: "1.1.3"
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: "4.5.6"
    compile group: 'org.apache.httpcomponents', name: 'httpcore', version: "4.4.10"
    compile group: 'org.apache.tika', name: 'tika-core', version: "1.10"
    compile group: 'org.jcraft', name: 'jorbis', version: "0.0.17"
    //compile group: 'org.jxmpp', name: 'jxmpp-core', version: "0.4.2"
    //compile group: 'org.jxmpp', name: 'jxmpp-util-cache', version: "0.4.2"
    compile group: 'org.jxmpp', name: 'jxmpp-jid', version: "0.4.2"
    compile group: 'net.sf.kxml', name: 'kxml2', version: "2.3.0"
    // missing <scope> for junit in json-simple's pom
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: "1.1.1", transitive: false
    compile group: 'com.googlecode.libphonenumber', name: 'libphonenumber', version: "7.0.8"
    compile group: 'org.ocpsoft.prettytime', name: 'prettytime', version: "3.2.7.Final"
    compile group: 'org.igniterealtime.smack', name: 'smack-core', version: "$smackVersion"
    compile group: 'org.igniterealtime.smack', name: 'smack-extensions', version: "$smackVersion"
    compile group: 'org.igniterealtime.smack', name: 'smack-im', version: "$smackVersion"
    compile group: 'org.igniterealtime.smack', name: 'smack-java7', version: "$smackVersion"
    compile group: 'org.igniterealtime.smack', name: 'smack-tcp', version: "$smackVersion"
    compile group: 'org.xerial', name: 'sqlite-jdbc', version: "3.8.6"
    compile group: 'de.sciss', name: 'weblaf', version: "1.28"

    testCompile group: 'junit', name: 'junit', version: "4.12"
}

repositories {
    mavenCentral()
}

run {
    if (project.hasProperty('jvmargs')) {
        jvmArgs(jvmargs.split(','))
    }
}

task deleteDeps(type: Delete) {
  delete fileTree(project.file('dist/lib')) {
        include '*.jar'
    }
}

task copyDeps(type: Copy) {
    from(configurations.runtime)
    into project.file('dist/lib')
    dependsOn ':deleteDeps'
}

jar {
    baseName = 'KontalkDesktopApp'
    destinationDir project.file('dist')
    
    manifest {
        attributes(
            "Main-Class": mainClassName,
            "Class-Path": configurations.compile.collect { 'lib/'+it.getName() }.join(' ')
        )
    }
    
    dependsOn ':copyDeps'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:3.3.1'
    }
}

apply plugin: 'org.owasp.dependencycheck'
